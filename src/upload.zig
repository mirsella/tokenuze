const std = @import("std");
const machine_id = @import("machine_id.zig");
const io_util = @import("io_util.zig");

pub const ProviderUpload = struct {
    name: []const u8,
    daily_summary: []const u8,
};

const DEFAULT_API_URL = "http://localhost:8000";
const empty_sessions_json = "{\"sessions\":[],\"totals\":{}}";
const empty_weekly_json = "{\"weekly\":[]}";

pub fn run(allocator: std.mem.Allocator, providers: []const ProviderUpload) !void {
    var env = try EnvConfig.load(allocator);
    defer env.deinit(allocator);

    if (env.api_key.len == 0) reportMissingApiKey();

    const endpoint = try buildEndpoint(allocator, env.api_url);
    defer allocator.free(endpoint);

    const machine = try machine_id.getMachineId(allocator);
    const machine_slice = machine[0..];
    std.log.info("Machine ID: {s}", .{machine_slice});

    if (providers.len == 0) return error.NoProvidersSelected;

    const payload = try buildUploadPayload(allocator, machine_slice, providers);
    defer allocator.free(payload);

    std.log.info("Uploading summary to {s}...", .{endpoint});
    var response = sendPayload(allocator, endpoint, env.api_key, payload) catch |err| {
        std.log.err("Connection failed. Is the server running at {s}? ({s})", .{ env.api_url, @errorName(err) });
        return err;
    };
    defer response.deinit(allocator);

    handleResponse(response);
}

const EnvConfig = struct {
    api_url: []const u8,
    api_key: []const u8,

    fn load(allocator: std.mem.Allocator) !EnvConfig {
        const raw_url = std.process.getEnvVarOwned(allocator, "DASHBOARD_API_URL") catch |err| switch (err) {
            error.EnvironmentVariableNotFound => try allocator.dupe(u8, DEFAULT_API_URL),
            else => return err,
        };
        const trimmed_url = std.mem.trim(u8, raw_url, " \n\r\t");
        const api_url = try allocator.dupe(u8, trimmed_url);
        allocator.free(raw_url);

        const raw_key = std.process.getEnvVarOwned(allocator, "DASHBOARD_API_KEY") catch |err| switch (err) {
            error.EnvironmentVariableNotFound => try allocator.dupe(u8, ""),
            else => return err,
        };
        const trimmed_key = std.mem.trim(u8, raw_key, " \n\r\t");
        const api_key = try allocator.dupe(u8, trimmed_key);
        allocator.free(raw_key);

        return .{ .api_url = api_url, .api_key = api_key };
    }

    fn deinit(self: *EnvConfig, allocator: std.mem.Allocator) void {
        allocator.free(self.api_url);
        allocator.free(self.api_key);
        self.* = undefined;
    }
};

fn reportMissingApiKey() noreturn {
    std.debug.print("Error: DASHBOARD_API_KEY not set\n", .{});
    std.debug.print("   Please add to ~/.zshrc or ~/.bashrc:\n", .{});
    std.debug.print("   export DASHBOARD_API_KEY=\"sk_team_your_api_key_here\"\n", .{});
    std.debug.print("   (optionally set DASHBOARD_API_URL for non-default endpoints)\n", .{});
    std.process.exit(1);
}

const HttpResponse = struct {
    status: std.http.Status,
    body: []u8,

    fn deinit(self: *HttpResponse, allocator: std.mem.Allocator) void {
        allocator.free(self.body);
        self.* = undefined;
    }
};

fn sendPayload(
    allocator: std.mem.Allocator,
    endpoint: []const u8,
    api_key: []const u8,
    payload: []u8,
) !HttpResponse {
    var io_single = std.Io.Threaded.init_single_threaded;
    defer io_single.deinit();

    var client = std.http.Client{
        .allocator = allocator,
        .io = io_single.io(),
    };
    defer client.deinit();

    var extra_headers = [_]std.http.Header{
        .{ .name = "X-API-Key", .value = api_key },
    };

    const uri = try std.Uri.parse(endpoint);
    const protocol = std.http.Client.Protocol.fromUri(uri) orelse return error.UnsupportedUriScheme;

    var host_buffer: [std.Io.net.HostName.max_len]u8 = undefined;
    const host_name = try uri.getHost(&host_buffer);

    return sendWithFallback(
        allocator,
        &client,
        uri,
        protocol,
        host_name,
        &extra_headers,
        payload,
    );
}

fn sendWithFallback(
    allocator: std.mem.Allocator,
    client: *std.http.Client,
    uri: std.Uri,
    protocol: std.http.Client.Protocol,
    host_name: std.Io.net.HostName,
    extra_headers: []const std.http.Header,
    payload: []u8,
) !HttpResponse {
    return sendOnce(client, uri, extra_headers, payload, null) catch |err| switch (err) {
        error.InvalidDnsCnameRecord => {
            const connection = try connectWithLibcResolver(allocator, client, uri, protocol, host_name) orelse return err;
            return sendOnce(client, uri, extra_headers, payload, connection);
        },
        else => return err,
    };
}

fn sendOnce(
    client: *std.http.Client,
    uri: std.Uri,
    extra_headers: []const std.http.Header,
    payload: []u8,
    connection: ?*std.http.Client.Connection,
) !HttpResponse {
    var request = try client.request(.POST, uri, .{
        .headers = .{ .content_type = .{ .override = "application/json" } },
        .extra_headers = extra_headers,
        .connection = connection,
        .keep_alive = false,
    });
    defer request.deinit();

    try request.sendBodyComplete(payload);
    var response = try request.receiveHead(&.{});

    var transfer_buffer: [4096]u8 = undefined;
    const reader = response.reader(&transfer_buffer);
    const body = try reader.allocRemaining(client.allocator, std.Io.Limit.limited(1024 * 1024));

    return .{
        .status = response.head.status,
        .body = body,
    };
}

fn connectWithLibcResolver(
    allocator: std.mem.Allocator,
    client: *std.http.Client,
    uri: std.Uri,
    protocol: std.http.Client.Protocol,
    host_name: std.Io.net.HostName,
) !?*std.http.Client.Connection {
    const port = uri.port orelse defaultPort(protocol);
    const ipv4 = try resolveIpv4Address(allocator, host_name.bytes, port) orelse return null;

    var literal_buffer: [16]u8 = undefined;
    const literal = try std.fmt.bufPrint(&literal_buffer, "{d}.{d}.{d}.{d}", .{
        ipv4[0],
        ipv4[1],
        ipv4[2],
        ipv4[3],
    });

    const ip_host = std.Io.net.HostName.init(literal) catch return null;
    return client.connectTcpOptions(.{
        .host = ip_host,
        .port = port,
        .protocol = protocol,
        .proxied_host = host_name,
    }) catch |err| switch (err) {
        error.InvalidDnsCnameRecord => return null,
        else => return err,
    };
}

fn resolveIpv4Address(
    allocator: std.mem.Allocator,
    host: []const u8,
    port: u16,
) !?[4]u8 {
    if (@import("builtin").target.os.tag == .windows) {
        return null;
    }

    const host_c = try std.mem.concatWithSentinel(allocator, u8, &.{host}, 0);
    defer allocator.free(host_c);

    var port_buffer: [8:0]u8 = undefined;
    const port_c = std.fmt.bufPrintZ(&port_buffer, "{d}", .{port}) catch unreachable;

    var hints: std.posix.addrinfo = .{
        .flags = .{},
        .family = std.posix.AF.INET,
        .socktype = std.posix.SOCK.STREAM,
        .protocol = std.posix.IPPROTO.TCP,
        .addrlen = 0,
        .addr = null,
        .canonname = null,
        .next = null,
    };

    var results: ?*std.posix.addrinfo = null;
    const rc = std.posix.system.getaddrinfo(host_c.ptr, port_c.ptr, &hints, &results);
    defer if (results) |ptr| std.posix.system.freeaddrinfo(ptr);

    if (@intFromEnum(rc) != 0) return null;

    var cursor = results;
    while (cursor) |entry| : (cursor = entry.next) {
        const sockaddr = entry.addr orelse continue;
        var storage: std.posix.sockaddr.in = undefined;
        const src_bytes = @as([*]const u8, @ptrCast(sockaddr))[0..@sizeOf(std.posix.sockaddr.in)];
        @memcpy(std.mem.asBytes(&storage), src_bytes);
        return @bitCast(storage.addr);
    }

    return null;
}

fn buildEndpoint(allocator: std.mem.Allocator, base: []const u8) ![]u8 {
    const trimmed = trimTrailingSlash(base);
    return std.fmt.allocPrint(allocator, "{s}/api/usage/report", .{trimmed});
}

fn buildUploadPayload(
    allocator: std.mem.Allocator,
    machine: []const u8,
    providers: []const ProviderUpload,
) ![]u8 {
    const timestamp = try currentTimestampIso8601(allocator);
    defer allocator.free(timestamp);

    const hostname = try resolveHostname(allocator);
    defer allocator.free(hostname);

    const username = try resolveUsername(allocator);
    defer allocator.free(username);

    const display_name = try std.fmt.allocPrint(allocator, "{s}@{s}", .{ username, hostname });
    defer allocator.free(display_name);

    const payload = Payload{
        .timestamp = timestamp,
        .machineId = machine,
        .hostname = hostname,
        .displayName = display_name,
        .providers = providers,
    };

    var buffer = std.ArrayList(u8).empty;
    defer buffer.deinit(allocator);
    var writer_state = io_util.ArrayWriter.init(&buffer, allocator);
    var stringify = std.json.Stringify{ .writer = writer_state.writer(), .options = .{} };
    try stringify.write(payload);
    return buffer.toOwnedSlice(allocator);
}

fn defaultPort(protocol: std.http.Client.Protocol) u16 {
    return switch (protocol) {
        .plain => 80,
        .tls => 443,
    };
}

fn trimTrailingSlash(value: []const u8) []const u8 {
    var end = value.len;
    while (end > 0 and value[end - 1] == '/') : (end -= 1) {}
    return value[0..end];
}

fn handleResponse(response: HttpResponse) void {
    switch (response.status) {
        .ok => {
            std.log.info("Usage reported successfully", .{});
        },
        .unauthorized => {
            std.log.err("Authentication failed: Invalid or inactive API key", .{});
        },
        .unprocessable_entity => {
            std.log.err("Data validation error. See server logs for details.", .{});
        },
        .internal_server_error => {
            std.log.err("Server error. Please try again later.", .{});
        },
        else => {
            std.log.err("Failed to report usage (HTTP {d}). Check server logs for diagnostics.", .{@intFromEnum(response.status)});
        },
    }
}

const Payload = struct {
    timestamp: []const u8,
    machineId: []const u8,
    hostname: []const u8,
    displayName: []const u8,
    providers: []const ProviderUpload,

    pub fn jsonStringify(self: Payload, jw: anytype) !void {
        try jw.beginObject();
        try jw.objectField("timestamp");
        try jw.write(self.timestamp);
        try jw.objectField("machineId");
        try jw.write(self.machineId);
        try jw.objectField("hostname");
        try jw.write(self.hostname);
        try jw.objectField("displayName");
        try jw.write(self.displayName);

        for (self.providers) |provider| {
            try jw.objectField(provider.name);
            try jw.beginObject();
            try jw.objectField("sessions");
            try jw.write(RawJson{ .text = empty_sessions_json });
            try jw.objectField("daily");
            const trimmed_daily = std.mem.trim(u8, provider.daily_summary, " \n\r\t");
            try jw.write(RawJson{ .text = trimmed_daily });
            try jw.objectField("weekly");
            try jw.write(RawJson{ .text = empty_weekly_json });
            try jw.endObject();
        }

        try jw.endObject();
    }
};

const RawJson = struct {
    text: []const u8,

    pub fn jsonStringify(self: RawJson, jw: anytype) !void {
        try jw.beginWriteRaw();
        try jw.writer.writeAll(self.text);
        jw.endWriteRaw();
    }
};

fn resolveHostname(allocator: std.mem.Allocator) ![]u8 {
    if (std.process.getEnvVarOwned(allocator, "HOSTNAME")) |value| {
        return value;
    } else |err| switch (err) {
        error.EnvironmentVariableNotFound => {},
        else => return err,
    }

    if (std.process.getEnvVarOwned(allocator, "COMPUTERNAME")) |value| {
        return value;
    } else |err| switch (err) {
        error.EnvironmentVariableNotFound => {},
        else => return err,
    }

    if (@import("builtin").target.os.tag == .windows) {
        return allocator.dupe(u8, "unknown-host");
    } else {
        var buf: [std.posix.HOST_NAME_MAX]u8 = undefined;
        const host = std.posix.gethostname(&buf) catch {
            return allocator.dupe(u8, "unknown-host");
        };
        return allocator.dupe(u8, host);
    }
}

fn resolveUsername(allocator: std.mem.Allocator) ![]u8 {
    if (std.process.getEnvVarOwned(allocator, "USER")) |value| {
        return value;
    } else |err| switch (err) {
        error.EnvironmentVariableNotFound => {},
        else => return err,
    }

    if (std.process.getEnvVarOwned(allocator, "USERNAME")) |value| {
        return value;
    } else |err| switch (err) {
        error.EnvironmentVariableNotFound => {},
        else => return err,
    }

    return allocator.dupe(u8, "unknown");
}

fn currentTimestampIso8601(allocator: std.mem.Allocator) ![]u8 {
    const secs = currentUnixSeconds() catch return allocator.dupe(u8, "1970-01-01T00:00:00Z");
    const epoch = std.time.epoch.EpochSeconds{ .secs = secs };
    const epoch_day = epoch.getEpochDay();
    const year_day = epoch_day.calculateYearDay();
    const month_day = year_day.calculateMonthDay();
    const day_seconds = epoch.getDaySeconds();
    return std.fmt.allocPrint(
        allocator,
        "{d:0>4}-{d:0>2}-{d:0>2}T{d:0>2}:{d:0>2}:{d:0>2}Z",
        .{
            year_day.year,
            month_day.month.numeric(),
            @as(u8, month_day.day_index) + 1,
            day_seconds.getHoursIntoDay(),
            day_seconds.getMinutesIntoHour(),
            day_seconds.getSecondsIntoMinute(),
        },
    );
}

fn currentUnixSeconds() !u64 {
    const os = @import("builtin").target.os.tag;
    return switch (os) {
        .windows => windowsUnixSeconds(),
        else => posixUnixSeconds(),
    };
}

fn posixUnixSeconds() !u64 {
    const spec = try std.posix.clock_gettime(std.posix.CLOCK.REALTIME);
    const raw_secs = if (@hasField(std.posix.timespec, "tv_sec"))
        @field(spec, "tv_sec")
    else
        @field(spec, "sec");
    return @as(u64, @intCast(raw_secs));
}

fn windowsUnixSeconds() !u64 {
    const win = std.os.windows;
    const intervals = @as(u64, @intCast(win.ntdll.RtlGetSystemTimePrecise()));
    const WINDOWS_TO_UNIX_100NS = 11_644_473_600 * 10_000_000;
    return (intervals - WINDOWS_TO_UNIX_100NS) / 10_000_000;
}
